buildscript {
  repositories {
    mavenCentral()
    google()
  }
  dependencies {
    classpath libs.androidGradlePlugin
    classpath libs.maven.publish.gradlePlugin
    classpath libs.spotless.gradlePlugin
  }
}

allprojects {
  repositories {
    mavenCentral()
    google()
  }

  apply plugin: 'com.diffplug.spotless'
  spotless {
    java {
      target("src/**/*.java")
      targetExclude("src/**/FileProvider.java") // This is copied in from AOSP.
      removeUnusedImports()
      googleJavaFormat()
      formatAnnotations()
    }
  }
}

subprojects {
  plugins.withId('com.android.base') {
    android {
      defaultConfig {
        minSdk 14
        compileSdk 30
        targetSdkVersion 29

        group GROUP
        version VERSION_NAME
      }

      compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
      }
    }
  }
}
